apply plugin: 'com.android.application'

System.setProperty("appName", "sdktests")

if(file("../../../sxrsdk-demos/common.gradle").exists()) {
    apply from: '../../../sxrsdk-demos/common.gradle'
}


android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 28

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                arguments.add('-DINCLUDE_DIR='+rootProject.projectDir.absolutePath)
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    sourceSets.main {
        jni.srcDirs = [] // no auto generation of Android.mk
        jniLibs.srcDir 'jniLibs' // pre-compiled libraries
    }

    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
}

dependencies {
    if (project.hasProperty("useLocalDependencies") && project.useLocalDependencies)
    {
        if (findProject(':widgetLib')) {
            implementation project(':widgetLib')
        } else {
            implementation(name: 'widgetLib-debug', ext: 'aar')
        }
        if (findProject(':sxr-physics')) {
            implementation project(':sxr-physics')
        } else {
            implementation(name: 'sxr-physics-debug', ext: 'aar')
        }
    }
    else
    {
        implementation "com.samsungxr.widgetlib:widgetLib:$sxrVersion"
        implementation "com.samsungxr.physics:sxr-physics:$sxrVersion"
    }
    androidTestImplementation "androidx.annotation:annotation:1.0.0"
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'net.jodah:concurrentunit:0.4.2'
    implementation project(path: ':sxr-unittestutils')
}

android.applicationVariants.all { variant ->
    if (variant.getBuildType().name == "debug") {
        task "configDevice${variant.name.capitalize()}" (type: Exec){
            dependsOn variant.install

            group = 'Device Configuration'
            description = 'Grants android permissions before running the application.'

            def adb = android.getAdbExe().toString()
            def permissions = [ 'android.permission.READ_EXTERNAL_STORAGE','android.permission.WRITE_EXTERNAL_STORAGE']
            for ( permission in permissions ) {
                commandLine "$adb shell pm grant ${variant.applicationId} $permission".split(' ')
            }
        }
        variant.testVariant.connectedInstrumentTest.dependsOn "configDevice${variant.name.capitalize()}"
    }
}
